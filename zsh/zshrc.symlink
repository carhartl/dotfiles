# Interactive environment configs
setopt NULLGLOB EXTENDEDGLOB

# All of our non-path/env/profile/login/logout zsh files
typeset -U interactive_files completion_files
interactive_files=($DOTFILES/**/*.zsh~**/completion.zsh~**/path.zsh~**/env.zsh~**/profile.zsh~**/login.zsh~**/logout.zsh)
completion_files=($DOTFILES/**/completion.zsh)

# Load interactive files (everything but the completion, path, env,
# profile, login, and logout files)
for file in ${interactive_files}; do
  source "$file"
done

# Initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# Load every completion after autocomplete loads
for file in ${completion_files}; do
  source "$file"
done

unset interactive_files completion_files
unsetopt NULLGLOB EXTENDEDGLOB

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
# Include *after* all other configuration has been loaded so I can override
# certain options easily.
if [[ -a ~/.localrc ]]; then
  source ~/.localrc
fi

# Quote pasted URLs
autoload -U url-quote-magic
zle -N self-insert url-quote-magic

# Fun
autoload -U tetris
zle -N tetris
bindkey ^T tetris

# Make colors available for prompt
autoload colors && colors

# Setup prompt
export PROMPT=$'%(?.\xe2\x9a\xa1\xef\xb8\x8f.\xf0\x9f\x92\xa9) '

precmd() {
  # Sets the Terminal.app current working directory before the prompt is
  # displayed (for when opening a new tab).
  printf '\e]7;%s\a' "file://${HOST}${PWD// /%20}"

  # Instead of setting up prompt with a newline: setup elobarote information before prompt in here
  # to prevent redraw issues when text in prompt becomes too long...
  # http://superuser.com/questions/382503/how-can-i-put-a-newline-in-my-zsh-prompt-without-causing-terminal-redraw-issues
  print -rP '%n@%m:%{$fg[cyan]%}${PWD/$HOME/~}%{$reset_color%}'"\$(git-radar --zsh --fetch)"
}

preexec() {
  # Unsets the Terminal.app current working directory when a terminal
  # multiplexer or remote connection is started since it can no longer be
  # updated, and it becomes confusing when the directory displayed in the title
  # bar is no longer synchronized with real current working directory.
  if [[ "${2[(w)1]:t}" == (screen|tmux|dvtm|ssh|mosh) ]]; then
    print '\e]7;\a'
  fi
}

source /usr/local/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
