# Interactive environment configs
setopt NULLGLOB EXTENDEDGLOB

# All of our non-path/env/profile/login/logout zsh files
typeset -U interactive_files completion_files
interactive_files=($DOTFILES/*/*.zsh~*/completion.zsh~*/path.zsh~*/env.zsh~*/profile.zsh~*/login.zsh~*/logout.zsh)
completion_files=($DOTFILES/*/completion.zsh)

# Load interactive files (everything but the completion, path, env,
# profile, login, and logout files)
for file in ${interactive_files}; do
  source "$file"
done

# Initialize autocomplete here, otherwise functions won't be loaded
autoload -Uz compinit
if [ $(date +'%j') != $(stat -f '%Sm' -t '%j' ~/.zcompdump) ]; then
  compinit
else
  compinit -C
fi

# Load every completion after autocomplete loads
for file in ${completion_files}; do
  source "$file"
done

unset interactive_files completion_files
unsetopt NULLGLOB EXTENDEDGLOB

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
# Include *after* all other configuration has been loaded so I can override
# certain options easily.
if [[ -a ~/.localrc ]]; then
  source ~/.localrc
fi

# Quote pasted URLs
autoload -U url-quote-magic
zle -N self-insert url-quote-magic

# Fun
autoload -U tetris
zle -N tetris
bindkey ^T tetris

# Make colors available for prompt
autoload colors && colors

# Setup prompt
autoload -U promptinit; promptinit
prompt pure

source "/usr/local/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh"
source "/usr/local/etc/profile.d/z.sh"
